plugins {
    id 'java'
    id 'maven-publish'
    id 'io.github.goooler.shadow' version '8.1.8'
}

group = 'net.zithium.library'
version = '2.1.5'

repositories {
    mavenCentral()
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
    maven { url = 'https://libraries.minecraft.net/' }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT'
    compileOnly 'com.mojang:authlib:1.5.21'

    // For MiniMessage text formatting.
    implementation "net.kyori:adventure-text-minimessage:4.21.0"
    implementation "net.kyori:adventure-text-serializer-legacy:4.21.0"
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

build {
    dependsOn(shadowJar)
}

shadowJar {
    dependencies {
        include(dependency("net.kyori:adventure-text-minimessage"))
        include(dependency("net.kyori:adventure-text-serializer-legacy"))
    }

    
    relocate "net.kyori.adventure.text", "net.zithium.library.shaded.adventure"
    archiveFileName="ZithiumLibrary-${project.version}.jar"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId = group
            artifactId = project.name
            version = project.version
        }
    }
    repositories {
        maven {
            url 'https://jitpack.io'
        }
    }
}
