plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'net.zithium.library'
version = '1.1.0'

repositories {
    mavenCentral()
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
    maven { url = 'https://libraries.minecraft.net/' }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT")
    compileOnly 'com.mojang:authlib:1.5.21'

    // For MiniMessage text formatting.
    implementation "net.kyori:adventure-text-minimessage:4.14.0"
    implementation "net.kyori:adventure-text-serializer-legacy:4.14.0"
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

build {
    dependsOn(shadowJar)
}

shadowJar {
    dependencies {
        include(dependency("net.kyori:adventure-text-minimessage"))
        include(dependency("net.kyori:adventure-text-serializer-legacy"))
    }

    relocate "net.kyori.adventure.text.minimessage", "net.zithium.library.shaded.minimessage"
    relocate "net.kyori.adventure.text.serializer.legacy", "net.zithium.library.shaded.legacyserializer"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId = group
            artifactId = project.name
            version = project.version
        }
    }
    repositories {
        maven {
            url 'https://jitpack.io'
        }
    }
}